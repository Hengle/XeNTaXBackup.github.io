## Post #1
- Username: TomWin
- Rank: veteran
- Number of posts: 146
- Joined date: Mon Apr 12, 2010 2:46 am
- Post datetime: 2014-05-23T14:21:40+00:00
- Post Title: .aem (Android games)

I found in couple of android games, .aem extension

I upload model from Sport Cars Challenge
[http://www.sendspace.com/file/1k1k9o](http://www.sendspace.com/file/1k1k9o)
## Post #2
- Username: Chipicao
- Rank: ultra-veteran
- Number of posts: 476
- Joined date: Thu Feb 03, 2011 6:18 pm
- Post datetime: 2014-05-23T20:49:01+00:00
- Post Title: .aem (Android games)

I researched Sports Car Challenge a long time ago, but the format is stupid. Every triangle is separated from the mesh; there are no triangle strips, only single triangles. So the mesh ends up having lots of redundant vertices.
Whoever made this was either on drugs or didn't know what they were doing.
Plus the meshes aren't very good anyway, low details and lots of smoothing issues.
[](http://www.imagebam.com/image/8c346b328343473) [](http://www.imagebam.com/image/7b83de328343477) [](http://www.imagebam.com/image/7d86bb328343480) 

```

f = fopen aemFile "rb"

fseek f 24 #seek_set
numinds = readlong f #unsigned

farr = #()
for i=1 to numinds/3 do
(
	i1 = readshort f #unsigned
	i2 = readshort f #unsigned
	i3 = readshort f #unsigned
	append farr [i1, i2, i3]
)

varr = #()
for i=1 to numinds do
(
	vx = readfloat f
	vy = readfloat f
	vz = readfloat f
	append varr [vx, -vz, vy]
)

tarr = #()
for i=1 to numinds do
(
	tx = readfloat f
	ty = readfloat f
	append tarr [tx, ty, 0]
)

narr = #()
for i=1 to numinds do
(
	nx = readfloat f
	ny = readfloat f
	nz = readfloat f
	append narr [nx, -nz, ny]
)

carr = #()
for i=1 to numinds do
(
	r = readfloat f
	g = readfloat f
	b = readfloat f
	a = readfloat f
	append carr [r * 255, g * 255, b * 255, a * 255]
)
fclose f

max modify mode
cui.expertModeOn()
with redraw off
(
	amesh = mesh vertices:varr faces:farr
	
	meshop.setMapSupport amesh 0 true
	for i = 1 to numinds do setVertColor amesh i carr[i]
	
	meshop.setMapSupport amesh 1 true
	setMesh amesh tverts:tarr
	
	for face = 1 to amesh.numfaces do setFaceSmoothGroup amesh face 1
	select amesh
	addmodifier amesh (Edit_Normals ()) ui:off
	amesh.Edit_Normals.MakeExplicit selection:#{1..nArr.count}
	EN_convertVS = amesh.Edit_Normals.ConvertVertexSelection
	EN_setNormal = amesh.Edit_Normals.SetNormal
	normID = #{}
	--apply normals
	for v = 1 to nArr.count do
	(
		free normID
		EN_convertVS #{v} &normID
		for id in normID do EN_setNormal id nArr[v]
	)
	collapseStack amesh
	max select none
)
cui.expertModeOff()
```
## Post #3
- Username: huckleberrypie
- Rank: ultra-veteran
- Number of posts: 351
- Joined date: Mon Apr 26, 2010 1:51 pm
- Post datetime: 2014-05-24T01:07:14+00:00
- Post Title: .aem (Android games)

Asphalt 8 and Real Racing 3 had way better models than this lol.
