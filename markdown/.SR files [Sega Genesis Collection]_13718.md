## Post #1
- Username: Lance Boyle
- Rank: ultra-n00b
- Number of posts: 5
- Joined date: Thu Dec 24, 2015 1:41 pm
- Post datetime: 2015-12-24T06:00:16+00:00
- Post Title: .SR files [Sega Genesis Collection]

So I recently got an ISO of the PSP version of Sega Genesis Collection, to try and rip the menu music. While searching for it, I noticed that nearly all of the files are in a format called .SR, and one file, SEGA_FFE.sr, is the one I presumed that would have that file I'm looking for. Sadly, to my knowledge there's no way to extract files from them, and from what I've been looking up online, which was mostly results about other games that used the format, mainly Midway Arcade Treasures 2, but nothing that would suggest extracting files inside those.

I can at least confirm that the file I'm looking for is in that one, as one thread I found suggested looking around the files with a hex editor, and sure enough, I opened up SEGA_FFE.sr on Notepad++ and saw that the menu music seems to be there, as FRONTEND.at3, but, as I said, there doesn't seem to be a way to extract it, to my knowledge.

Does anyone know if that's possible? I am including a link to the actual file itself ([here](https://www.dropbox.com/s/oapyb9pc2ml2bs3/SEGA_FFE.SR?dl=0)) as it exceeds the attachment size limit. It's from the PSP version, as previously mentioned, but if someone extracts from the PS2 version's version of that file, I'd be grateful either way.

Thanks in advance.
## Post #2
- Username: barti
- Rank: veteran
- Number of posts: 148
- Joined date: Sun Apr 01, 2012 7:44 pm
- Post datetime: 2015-12-24T22:58:42+00:00
- Post Title: .SR files [Sega Genesis Collection]

Funny format, header is stored entirely in text form   
And they did something weird to the WAV files, I could assume they're headerless PCM or AT3 but I'll have to check tomorrow.

EDIT: Fixed .WAV extraction, now they have a proper RIFF header 
EDIT 2: Changed 's' operator to 'S' for correct name extraction (Thanks aluigi)

```

# make sure QuickBMS is up-to-date ( important string split operator bugfix )
QuickBMSVer 0.7.0

getct HEADER string 0x0a
string ELEMENTS S HEADER FILES DUMMY START_OFF

for i = 0 < FILES
	getct ENTRY string 0x0a
	string ELEMENTS S ENTRY OFFSET SIZE CHANNELS FREQUENCY BITS NAME

	math OFFSET + START_OFF

	if CHANNELS < 100 && BITS > 0
		log MEMORY_FILE2 OFFSET SIZE
		callfunction raw2wav
	else
		log NAME OFFSET SIZE
	endif
next i

# raw2wav.bms by aluigi
startfunction raw2wav
	set MEMORY_FILE binary "\x52\x49\x46\x46\x00\x00\x00\x00\x57\x41\x56\x45\x66\x6d\x74\x20\x10\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x64\x61\x74\x61\x00\x00\x00\x00"

	set RIFFSIZE long SIZE
	math RIFFSIZE += 36
	set BLOCKALIGN long BITS
	set AVGBYTES long FREQUENCY
	math BLOCKALIGN /= 8
	math BLOCKALIGN *= CHANNELS
	math AVGBYTES *= BLOCKALIGN

	putvarchr MEMORY_FILE 4 RIFFSIZE long
	putvarchr MEMORY_FILE 20 1 short          # wFormatTag: Microsoft PCM Format (0x0001)
	putvarchr MEMORY_FILE 22 CHANNELS short   # wChannels
	putvarchr MEMORY_FILE 24 FREQUENCY long   # dwSamplesPerSec
	putvarchr MEMORY_FILE 28 AVGBYTES long    # dwAvgBytesPerSec
	putvarchr MEMORY_FILE 32 BLOCKALIGN short # wBlockAlign
	putvarchr MEMORY_FILE 34 BITS short       # wBitsPerSample
	putvarchr MEMORY_FILE 40 SIZE long

	append
	log MEMORY_FILE 0 SIZE MEMORY_FILE2
	append

	get SIZE asize MEMORY_FILE
	log NAME 0 SIZE MEMORY_FILE
endfunction
```
## Post #3
- Username: Lance Boyle
- Rank: ultra-n00b
- Number of posts: 5
- Joined date: Thu Dec 24, 2015 1:41 pm
- Post datetime: 2015-12-26T21:12:40+00:00
- Post Title: .SR files [Sega Genesis Collection]

That worked! Thanks, man!
