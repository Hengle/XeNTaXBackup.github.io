## Post #1
- Username: Sigil
- Rank: ultra-n00b
- Number of posts: 5
- Joined date: Wed Jul 08, 2015 4:21 am
- Post datetime: 2015-07-07T22:40:13+00:00
- Post Title: .tex files from unity.bms

I'm trying to figure out how to open the .tex files that result from the extracting unity .assets files with the unity.bms QuickBMS script. Specifically I'd like to open the icons from the game 'Ori and the Blind Forest', though a generic conversion tool would be nice if there is such a thing.

I looked at the hex some and noticed these things:

0x0-0x7: 1024x1024? Seems a bit large for what this is, but then again it is a relative big icon in game and they might have wanted it to scale well on 4k monitors.
0x8: 0x155570 size of file - header size
0xC-0x33: ???
0x34: 0x155570 size of file - header size
[hex.png](https://xentaxbackup.github.io/file/9400_hex.png)
## Post #2
- Username: barti
- Rank: veteran
- Number of posts: 148
- Joined date: Sun Apr 01, 2012 7:44 pm
- Post datetime: 2015-07-08T09:05:54+00:00
- Post Title: .tex files from unity.bms

I made this quickBMS script since I wanted to extract textures from a Unity 5 game once (and there aren't any working Unity 5 texture extractors that I know of), but it's by no means universal. Still worth at least a try.

```
string NAME += ".tga"

get WIDTH long
get HEIGHT long
get IMGSIZE long
goto 56

xmath PIXELS "IMGSIZE / 4"
math IMGSIZE += 18
log MEMORY_FILE 0 IMGSIZE

put 131072 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE
put WIDTH short MEMORY_FILE
put HEIGHT short MEMORY_FILE
put 32 byte MEMORY_FILE
put 8 byte MEMORY_FILE

for i = 0 < PIXELS
    get COLOR long
    reverselong COLOR
    put COLOR long MEMORY_FILE
next i

get SIZE asize MEMORY_FILE
log NAME 0 SIZE MEMORY_FILE
```
## Post #3
- Username: Sigil
- Rank: ultra-n00b
- Number of posts: 5
- Joined date: Wed Jul 08, 2015 4:21 am
- Post datetime: 2015-07-09T13:53:56+00:00
- Post Title: .tex files from unity.bms

That script didn't not work. These textures seem to be in DXT4 or DXT5 pixel format. I'll try using your script as an example to build a DDS header. I was toying around with doing so, but I'm new to this and didn't have a nice example of bms that just replaced the header, like yours does.
## Post #4
- Username: Sigil
- Rank: ultra-n00b
- Number of posts: 5
- Joined date: Wed Jul 08, 2015 4:21 am
- Post datetime: 2015-07-09T15:22:53+00:00
- Post Title: .tex files from unity.bms

I have this script working for the 1024x1024 images. It needs a bit more math to calculate pitch and mip map levels based on the size of the image, both these numbers are currently hard coded for 1024x1024 and don't seem to show up in the tex file's header. I'd also like it if it had a check that the current .tex file is actually .dds and skip it if it isn't, which I think the 4th long in a .tex file is a type field.

Also, for some reason when I try to pre-allocate the memory file I get an error about being unable to read an address. That showed up just from changing "math IMGSIZE += 18" to "math IMGSIZE += 500".

```
#
# script for QuickBMS http://quickbms.aluigi.org

get NAME basename
string NAME += ".dds"

get WIDTH long
get HEIGHT long
get IMGSIZE long
get TYPE long
get FLAGS long
goto 0x38

if TYPE = 0xC

math PIXELS = IMGSIZE
/* For some reason these two lines cause an error,
 * skipping pre-allocation for now
 */
#math IMGSIZE += 500
#log MEMORY_FILE 0 IMGSIZE
log MEMORY_FILE 0 1

xmath PITCH "WIDTH * HEIGHT"
set DDSFLAGS long 0x8100f
set MIPMAPS long 0

if FLAGS & 0x1
    math DDSFLAGS += 0x20000
    if WIDTH > HEIGHT
        for i = WIDTH > 0
            math MIPMAPS += 1
        next i / 2
    else
        for i = HEIGHT > 0
            math MIPMAPS += 1
        next i / 2
    endif
endif

#DDS Header
put 0x20534444 long MEMORY_FILE
put 124 long MEMORY_FILE
put 0xa1007 long MEMORY_FILE
put HEIGHT long MEMORY_FILE
put WIDTH long MEMORY_FILE
put PITCH long MEMORY_FILE
put 0 long MEMORY_FILE
put MIPMAPS long MEMORY_FILE
for i = 0 < 11
    put 0 long MEMORY_FILE
next i

#Pixel format struct
put 32 long MEMORY_FILE
put 0x4 long MEMORY_FILE
put 0x35545844 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE

#DDS Header cont.
put 0x401008 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE



for i = 0 < PIXELS
    get COLOR byte
    put COLOR byte MEMORY_FILE
next i

get SIZE asize MEMORY_FILE
log NAME 0 SIZE MEMORY_FILE

endif

```


EDIT:
It now works with any size image.
Pitch = Width * Height, rather than the formula specified by MS...
For mip map count I did a little loop. Hackish, but it works, at least for most files. If someone knows a saner way to do this in BMS I'd be glad to know.
I also put a check for the tex's type.
## Post #5
- Username: Sigil
- Rank: ultra-n00b
- Number of posts: 5
- Joined date: Wed Jul 08, 2015 4:21 am
- Post datetime: 2015-07-09T17:08:16+00:00
- Post Title: .tex files from unity.bms

Hmm... all the images are mirrored vertically. I didn't notice that on the couple images I was using to test while writing the script, because they didn't look wrong upside down. I feel like fixing that would require a LOT of work though, even though it'd be nice to get the whole process automated from start to finish.
## Post #6
- Username: Sigil
- Rank: ultra-n00b
- Number of posts: 5
- Joined date: Wed Jul 08, 2015 4:21 am
- Post datetime: 2015-07-09T17:33:33+00:00
- Post Title: .tex files from unity.bms

A 0x1 flag at 0x10 and/or 0x28 indicates the presence of mipmaps. I updated the script to account for this.
## Post #7
- Username: barti
- Rank: veteran
- Number of posts: 148
- Joined date: Sun Apr 01, 2012 7:44 pm
- Post datetime: 2015-07-10T21:36:16+00:00
- Post Title: .tex files from unity.bms

Cool to see that you got it working.
## Post #8
- Username: adabada
- Rank: ultra-n00b
- Number of posts: 7
- Joined date: Mon Oct 12, 2015 8:16 am
- Post datetime: 2015-10-12T01:46:53+00:00
- Post Title: .tex files from unity.bms

Howdy!

Where did you get the .tex format specification? I'm having trouble converting a .tex file extracted using unity.bms and I think it's because of the differences between DXT5, DXT1, etc.

When running your script, DXT5 .tex files convert successfully to dds, but the script only works for .tex files of TYPE 0x0000000c, and I have some files that have a TYPE of 0x00000003.

So far I've tested it with 3 TYPEs of files:

DXT5 (first 56 bytes):

```
00 02 00 00 00 02 00 00 00 00 04 00 0C 00 00 00 00 00 01 00 01 00 00 00 02 00 00 00 00 00 00 00 01 00 00 00 00 00 00 00 01 00 00 00 00 00 00 00 01 00 00 00 00 00 04 00
```


DXT1 (first 56 bytes):

```
00 02 00 00 00 02 00 00 B8 AA 02 00 0A 00 00 00 01 00 01 00 01 00 00 00 02 00 00 00 01 00 00 00 01 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 01 00 00 00 B8 AA 02 00
```


I have no idea what this is... R8G8B8? (first 56 bytes):

```
2A 03 00 00 2A 03 00 00 AC 08 1E 00 03 00 00 00 00 00 01 00 01 00 00 00 02 00 00 00 00 00 00 00 01 00 00 00 00 00 00 00 01 00 00 00 00 00 00 00 01 00 00 00 AC 08 1E 00
```


Can you point me in the right direction on how to convert those types of .tex to dds?
## Post #9
- Username: shakotay2
- Rank: MEGAVETERAN
- Number of posts: 4300
- Joined date: Fri Apr 20, 2012 4:24 pm
- Post datetime: 2015-10-12T10:50:54+00:00
- Post Title: .tex files from unity.bms

> Reply from adabada
>
> I have no idea what this is... R8G8B8? (first 56 bytes):
Code: Select all2A 03 00 00 2A 03 00 00 AC 08 1E 00 03 00 00 00 00 00 01 00 01 00 00 00 02 00 00 00 00 00 00 00 01 00 00 00 00 00 00 00 01 00 00 00 00 00 00 00 01 00 00 00 AC 08 1E 00

Can you point me in the right direction on how to convert those types of .tex to dds?if you uploaded a sample we maybe could tell...

btw: I successfully used barti's bms script on a type 4 file (plus some beautifying in texturefinder):



button-arrow-base_type4.JPG (33.18 KiB) Viewed 839 times


(the "noise" is from the bmp to jpg conversion)
## Post #10
- Username: adabada
- Rank: ultra-n00b
- Number of posts: 7
- Joined date: Mon Oct 12, 2015 8:16 am
- Post datetime: 2015-10-12T16:22:23+00:00
- Post Title: .tex files from unity.bms

Hello shakotay2, thank for the help!

Here goes some samples: [https://mega.nz/#!oQBEnJrY!BvyOV9SiKuph ... _YzgvGTRjo](https://mega.nz/#!oQBEnJrY!BvyOV9SiKuphFZEKiUy_6kboLJT35kWhh_YzgvGTRjo)
## Post #11
- Username: shakotay2
- Rank: MEGAVETERAN
- Number of posts: 4300
- Joined date: Fri Apr 20, 2012 4:24 pm
- Post datetime: 2015-10-12T19:32:17+00:00
- Post Title: .tex files from unity.bms

that's my nearest hit for your "R8G8B8":



test2_2.JPG (71.2 KiB) Viewed 829 times
## Post #12
- Username: adabada
- Rank: ultra-n00b
- Number of posts: 7
- Joined date: Mon Oct 12, 2015 8:16 am
- Post datetime: 2015-10-12T22:04:08+00:00
- Post Title: .tex files from unity.bms

Yeah, I got something similar to that as well when using the script, but Unity Asset Explorer by Haoose manages to convert it to dds. Here is the final image:



Capture.PNG (250.66 KiB) Viewed 826 times



And here is the DDS header for that image:

```
44 44 53 20 7C 00 00 00 07 10 00 00 2A 03 00 00 2A 03 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 20 00 00 00 40 00 00 00 00 00 00 00 18 00 00 00 00 00 FF 00 00 FF 00 00 FF 00 00 00 00 00 00 00 00 10 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00 00
```


Here's a link to the dds generates with Unity Asset Explorer: [https://mega.nz/#!0IBEmZaR!Bt27XlsYKGwq ... t4eotNtUZw](https://mega.nz/#!0IBEmZaR!Bt27XlsYKGwqoHhs5ba3wXAGICSua5m2Rt4eotNtUZw)
## Post #13
- Username: adabada
- Rank: ultra-n00b
- Number of posts: 7
- Joined date: Mon Oct 12, 2015 8:16 am
- Post datetime: 2015-10-15T22:23:47+00:00
- Post Title: .tex files from unity.bms

anyone?
## Post #14
- Username: shakotay2
- Rank: MEGAVETERAN
- Number of posts: 4300
- Joined date: Fri Apr 20, 2012 4:24 pm
- Post datetime: 2015-10-16T10:24:40+00:00
- Post Title: .tex files from unity.bms

anyone for what? Didn't Unity Asset Explorer solve your problem?
## Post #15
- Username: adabada
- Rank: ultra-n00b
- Number of posts: 7
- Joined date: Mon Oct 12, 2015 8:16 am
- Post datetime: 2015-10-16T15:14:23+00:00
- Post Title: .tex files from unity.bms

Not really, I'm trying to modify the script to convert the other types. Even tho UAE can convert them, It still can't open asset files from Unity 5.x without issues.

This was my original request for help:

> Where did you get the .tex format specification? I'm having trouble converting a .tex file extracted using unity.bms and I think it's because of the differences between DXT5, DXT1, etc.
## Post #16
- Username: shakotay2
- Rank: MEGAVETERAN
- Number of posts: 4300
- Joined date: Fri Apr 20, 2012 4:24 pm
- Post datetime: 2015-10-16T17:10:48+00:00
- Post Title: Re: .tex files from unity.bms

you're not to clear with your posts - you wrote

> When running your script, DXT5 .tex files convert successfully to dds
so DXT5 is solved, isn't it? (should not be too hard to add creating of a DXT5 header to Sigil's script - assumed what this script retrieves correct height&width from the tex files; didn't check that).

For the R8G8B8 (type 3) I used irfanview with the tga file created by barti's script like such:



R8G8B8.JPG (243.01 KiB) Viewed 406 times


So for this type "only" a palette problem remains but I'm too busy to deal with such.
edit: ok, stupid me forgot to switch back to 'RGB' in 'options for 24 and 32 BPP'
but not sure whether it's a palette problem or a matter of saturation.

Guess you're on your own to transform these "results" into a working script.
(except barti will sweep in again.    )
## Post #17
- Username: adabada
- Rank: ultra-n00b
- Number of posts: 7
- Joined date: Mon Oct 12, 2015 8:16 am
- Post datetime: 2015-10-16T19:31:29+00:00
- Post Title: Re: .tex files from unity.bms

> Reply from shakotay2
>
> you're not to clear with your posts - you wrote
When running your script, DXT5 .tex files convert successfully to dds
so DXT5 is solved, isn't it? (should not be too hard to add creating of a DXT5 header to Sigil's script - assumed what this script retrieves correct height&width from the tex files; didn't check that).

My original post said I was able to use the script to convert DXT5 .tex files into dds, but had issue with DXT1 and R8G8B8. I apologise if that was not clear enough.

Yes, DXT5 is solved, DXT1 and R8G8B8 are the problem. The script does indeed get the correct height, width and type from the tex files, but because I can't find documentation on the structure of .tex files I don't know what else I need to convert DXT1 and R8G8B8 .text files to dds.
## Post #18
- Username: adabada
- Rank: ultra-n00b
- Number of posts: 7
- Joined date: Mon Oct 12, 2015 8:16 am
- Post datetime: 2015-10-16T22:51:45+00:00
- Post Title: Re: .tex files from unity.bms

OK, DXT1 and DXT5 now are working. All that is left is this weird R8G8B8.
## Post #19
- Username: shakotay2
- Rank: MEGAVETERAN
- Number of posts: 4300
- Joined date: Fri Apr 20, 2012 4:24 pm
- Post datetime: 2015-10-17T22:11:44+00:00
- Post Title: Re: .tex files from unity.bms

This is a working conversion chain for R8G8B8.tex (type 3):
(image width/height: 0x0000: 2A0300002A030000 -> 810 x 810)
delete 56 bytes header from tex file
load it as raw file into irfanview and save it as tga for example
use TheCompressonator to convert the tga to whatever DXT format you wish

This is even quicker:
 rename R8G8B8.tex to R8G8B8.tex.data
 load it as "Raw image data" into gimp.
 set the offset to 56
 export from gimp as uncompressed dds

If you want to do this by script you need to dig into pixelformats.
With an 8x8 red pixels square (8x8x3=192=0xC0 bytes= 64x24 bits) it looks like this:



R8G8B8rawToDDS.JPG (222.63 KiB) Viewed 393 times

(red.dds: uncompressed)

The hurdle may be to convert the R8G8B8.tex into a plain 24 bit file as the red.raw above. -> solved

There's a good chance to get gimp's Raw image data format specs (should be the same as for R8G8B8.tex without header).

btw: the export from gimp as uncompressed dds gives the same result as with Unity Asset Explorer - only these 15 bytes differ:
 8: 0F 07
14: 7E 00
15: 09 00
1C: 01 00
20: 47 00 //G
21: 49 00 //I
22:4D 00 //M
23: 50 00 //P
24: 2D 00 //-
25: 44 00 //D
26: 44 00 //D
27: 53 00 //S
28: 01 00
29: 02 00
2A: 02 00

Then I realized that you simply have to reverse the color bytes such like this: 9C854A A38741 -> 4A859C 4187A3
so here, finally, the changed script for type 3 only. It's a mix of Sigil's and barti's codes:
For other .tex than the one we handled here check for the DDS header since mine is not reliable 

```
#
# script for QuickBMS http://quickbms.aluigi.org

get NAME basename
string NAME += ".dds"

get WIDTH long
get HEIGHT long
get IMGSIZE long
get TYPE long
get FLAGS long
goto 0x38

if TYPE = 3

xmath PIXELS "IMGSIZE / 3"
log MEMORY_FILE 0 1

xmath PITCH "WIDTH * HEIGHT"
set DDSFLAGS long 0x8100f
set MIPMAPS long 0

if FLAGS & 0x1
    math DDSFLAGS += 0x20000
    if WIDTH > HEIGHT
        for i = WIDTH > 0
            math MIPMAPS += 1
        next i / 2
    else
        for i = HEIGHT > 0
            math MIPMAPS += 1
        next i / 2
    endif
endif

#DDS Header
put 0x20534444 long MEMORY_FILE
put 124 long MEMORY_FILE
put 0xa1007 long MEMORY_FILE
put HEIGHT long MEMORY_FILE
put WIDTH long MEMORY_FILE
put PITCH long MEMORY_FILE
put 0 long MEMORY_FILE
put MIPMAPS long MEMORY_FILE
for i = 0 < 11
    put 0 long MEMORY_FILE
next i

#Pixel format struct
put 32 long MEMORY_FILE
put 0x40 long MEMORY_FILE
put 0 long MEMORY_FILE
put 24 long MEMORY_FILE

put 16711680 long MEMORY_FILE
put 65280 long MEMORY_FILE
put 255 long MEMORY_FILE
put 0 long MEMORY_FILE

#DDS Header cont.
put 0x001000 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE
put 0 long MEMORY_FILE

for i = 0 < PIXELS
    get C1 byte
    get C2 byte
    get C3 byte
    put C3 byte MEMORY_FILE
    put C2 byte MEMORY_FILE
    put C1 byte MEMORY_FILE
next i

get SIZE asize MEMORY_FILE
log NAME 0 SIZE MEMORY_FILE

endif
```
## Post #20
- Username: andeliseev
- Rank: ultra-n00b
- Number of posts: 1
- Joined date: Tue Dec 01, 2015 9:59 pm
- Post datetime: 2015-12-01T15:46:11+00:00
- Post Title: Re: .tex files from unity.bms

can you help with convert this file? i think its dxt1. but it simple dont work
[https://www.dropbox.com/s/eegk4xxuvfqykjs/11.data?dl=0](https://www.dropbox.com/s/eegk4xxuvfqykjs/11.data?dl=0)
## Post #21
- Username: shakotay2
- Rank: MEGAVETERAN
- Number of posts: 4300
- Joined date: Fri Apr 20, 2012 4:24 pm
- Post datetime: 2015-12-01T22:21:38+00:00
- Post Title: Re: .tex files from unity.bms

You guys - some bytes have changed and you are lost - aren't you?  

This is type 0x24.
Cut the first 20 bytes from 11.data.
Change Sigil's script (for type 0x0C) like such for type 0x24:

```
get HEIGHT long
get IMGSIZE long
get TYPE long
goto 0x28
get FLAGS long
goto 0x38

if TYPE = 0x24

math PIXELS = IMGSIZE
```

This is the result:



11-data_.jpg (223.51 KiB) Viewed 343 times



btw: 20 bytes is: 4 bytes string length (DW), 15 chars string, zero byte for termination
Will be different for other strings.
## Post #22
- Username: Azurfan
- Rank: advanced
- Number of posts: 61
- Joined date: Thu Jun 24, 2010 5:12 am
- Post datetime: 2015-12-13T13:32:53+00:00
- Post Title: Re: .tex files from unity.bms

I have problems with the DXT1 and DXT5 textures of Dreamfall Chapters, I got the dimensions working correctly but the rest is just a mess. The game uses Unity 5 since the last update, maybe that's part of the problem, I don't know.
[http://s000.tinyupload.com/index.php?fi ... 4668502156](http://s000.tinyupload.com/index.php?file_id=00983256764668502156)

Can anyone here share more insight on what I should do next to get this sorted out?
## Post #23
- Username: Acewell
- Rank: VIP member
- Number of posts: 1330
- Joined date: Wed Nov 05, 2008 7:16 pm
- Post datetime: 2015-12-13T14:16:12+00:00
- Post Title: Re: .tex files from unity.bms

TextureFinder reads your tex files as just dds with a custom header.
The eye is 512x512 dxt3 and the bark is 1024x1024 dxt1, both with mipmaps and shifted by 60.
## Post #24
- Username: Azurfan
- Rank: advanced
- Number of posts: 61
- Joined date: Thu Jun 24, 2010 5:12 am
- Post datetime: 2015-12-14T20:02:40+00:00
- Post Title: Re: .tex files from unity.bms

Thank you.
## Post #25
- Username: blackfoxeye
- Rank: veteran
- Number of posts: 110
- Joined date: Tue Mar 08, 2011 7:03 pm
- Post datetime: 2016-02-09T16:51:17+00:00
- Post Title: Re: .tex files from unity.bms

I have a .tex file which I want to convert.
I have tried the bms scripts provided here but those scripts are unable to convert this tex file.
This is from a game, which made with Unity 5.

When I tried "TextureFinder" tool I got below image.
I have attached the tex file here.



Can anybody please help, how to convert it correctly.
[Texture #008_512x512.45.zip](https://xentaxbackup.github.io/file/10457_Texture #008_512x512.45.zip)
## Post #26
- Username: Acewell
- Rank: VIP member
- Number of posts: 1330
- Joined date: Wed Nov 05, 2008 7:16 pm
- Post datetime: 2016-03-30T04:44:19+00:00
- Post Title: Re: .tex files from unity.bms

> Reply from Azurfan
>
> I have problems with the DXT1 and DXT5 textures of Dreamfall Chapters, I got the dimensions working correctly but the rest is just a mess. The game uses Unity 5 since the last update, maybe that's part of the problem, I don't know.
http://s000.tinyupload.com/index.php?fi ... 4668502156

Can anyone here share more insight on what I should do next to get this sorted out?

I made a Noesis plugin that opens your sample textures for fun  


 tex_DreamfallChapters_tex.zip
(656 Bytes) Downloaded 85 times
## Post #27
- Username: Tosyk
- Rank: double-veteran
- Number of posts: 1027
- Joined date: Thu Oct 22, 2009 5:24 pm
- Post datetime: 2016-07-21T18:45:19+00:00
- Post Title: Re: .tex files from unity.bms

playing around with tex files without any results. Guys please point me in the right direction with this files:

[https://dl.dropboxusercontent.com/u/991 ... .36.tex.7z](https://dl.dropboxusercontent.com/u/9919707/tex_cage_grunt_rita_suit_military_DIFF-SPEC%20%230370127_2048x2048.36.tex.7z)
[https://dl.dropboxusercontent.com/u/991 ... .36.tex.7z](https://dl.dropboxusercontent.com/u/9919707/Tex_Character_Napoleon_Solo_Stealth_D-S%20%230040252_1024x1024.36.tex.7z)

thanks in advanced.
## Post #28
- Username: dragonzh
- Rank: n00b
- Number of posts: 12
- Joined date: Wed Jul 29, 2015 1:23 pm
- Post datetime: 2016-07-23T03:32:44+00:00
- Post Title: Re: .tex files from unity.bms

> Reply from Tosyk
>
> playing around with tex files without any results. Guys please point me in the right direction with this files:

https://dl.dropboxusercontent.com/u/991 ... .36.tex.7z
https://dl.dropboxusercontent.com/u/991 ... .36.tex.7z

thanks in advanced.
Try UnityEX he is on site ZoG
[UnityEX ZoG](http://www.zoneofgames.ru/forum/index.php?showtopic=36240&pid=675422&st=200&#entry675422)
Only it is necessary to carry out the export of assets.
This is the format ktx, it is supported by console.
UnityEX export name.assets -t ktx
## Post #29
- Username: Tosyk
- Rank: double-veteran
- Number of posts: 1027
- Joined date: Thu Oct 22, 2009 5:24 pm
- Post datetime: 2016-07-23T15:26:49+00:00
- Post Title: Re: .tex files from unity.bms

> Reply from dragonzh
>
> Try UnityEX he is on site ZoG
UnityEX ZoG
Only it is necessary to carry out the export of assets.
This is the format ktx, it is supported by console.
UnityEX export name.assets -t ktxthanks, the tool works but I still can't open this ktx texture with PVRTexTool. I suppose they are encrypted or something. Can you take a look:
[https://dl.dropboxusercontent.com/u/991 ... tex.ktx.7z](https://dl.dropboxusercontent.com/u/9919707/tex_cage_grunt_rita_suit_military_DIFF-SPEC.tex.ktx.7z)
## Post #30
- Username: dragonzh
- Rank: n00b
- Number of posts: 12
- Joined date: Wed Jul 29, 2015 1:23 pm
- Post datetime: 2016-07-23T19:56:36+00:00
- Post Title: Re: .tex files from unity.bms

> Reply from Tosyk
>
> dragonzh wrote:Try UnityEX he is on site ZoG
UnityEX ZoG
Only it is necessary to carry out the export of assets.
This is the format ktx, it is supported by console.
UnityEX export name.assets -t ktxthanks, the tool works but I still can't open this ktx texture with PVRTexTool. I suppose they are encrypted or something. Can you take a look:
https://dl.dropboxusercontent.com/u/991 ... tex.ktx.7z
UnityEX 1.4.0
[tex_cage_grunt_rita_suit_military_DIFF-SPEC.tex.jpg](https://xentaxbackup.github.io/file/11345_tex_cage_grunt_rita_suit_military_DIFF-SPEC.tex.jpg)
## Post #31
- Username: Tosyk
- Rank: double-veteran
- Number of posts: 1027
- Joined date: Thu Oct 22, 2009 5:24 pm
- Post datetime: 2016-07-23T23:09:48+00:00
- Post Title: Re: .tex files from unity.bms

dragonzh,
thanks a lot. You supporting a great tool. I managed to convert these textures successfully.
## Post #32
- Username: dswd2015
- Rank: veteran
- Number of posts: 87
- Joined date: Fri Sep 04, 2015 5:33 am
- Post datetime: 2017-01-16T05:55:17+00:00
- Post Title: Re: .tex files from unity.bms

> Reply from dragonzh
>
> Tosyk wrote:dragonzh wrote:Try UnityEX he is on site ZoG
UnityEX ZoG
Only it is necessary to carry out the export of assets.
This is the format ktx, it is supported by console.
UnityEX export name.assets -t ktxthanks, the tool works but I still can't open this ktx texture with PVRTexTool. I suppose they are encrypted or something. Can you take a look:
https://dl.dropboxusercontent.com/u/991 ... tex.ktx.7z
UnityEX 1.4.0

Download link program
